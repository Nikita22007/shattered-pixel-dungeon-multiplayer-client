buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://storage.googleapis.com/r8-releases/raw")
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.9.1'
    }
}

allprojects {
    // load verson grom file
    def versionFile = new File(rootProject.rootDir, "version")
    def versionName = "Local-build"

    if (versionFile.exists()) {
        try {
            versionName = versionFile.text.trim()
        } catch (Exception e) {
            println("Error reading version file: ${e.message}")
        }
    } else {
        println("Version file not found, using default: '${versionName}'")
    }
    ext {
        appName = 'Shattered Pixel Dungeon Client'
        appPackageName = 'com.shatteredpixel.shatteredpixeldungeon.multiplayer.client'

        appVersionCode = 846
        appVersionName = '3.1.0'

        appVersionName = "Client_"+versionName + "_on_v"+appVersionName
        appShortVersionName = ((appVersionName) =~ /\d+\.\d+\.\d+/) ? ((appVersionName) =~ /\d+\.\d+\.\d+/)[0] : "0.0.0"

        appJavaCompatibility = JavaVersion.VERSION_1_8

        appAndroidCompileSDK = 35   //Android 15
        appAndroidMinSDK = 14       //Android 4.0
        //significant layout management changes required for API 35+ as edge-to-edge is forced on
        //will do this at the same time as dropping support for Android 4.4- (later in 2025)
        appAndroidTargetSDK = 34    //Android 14

        gdxVersion = '1.12.1'
        gdxControllersVersion = '2.2.4'
        robovmVersion = '2.3.21'
    }
    version = appVersionName

    repositories {
        google()
        mavenCentral()
        maven { url 'https://central.sonatype.com/repositories/maven-snapshots/' }
    }

}